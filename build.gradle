import java.nio.charset.StandardCharsets

plugins {
  id "java"
  id "org.sonarqube" version "2.8"
  id 'jacoco'
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'jacoco'

  group = "org.openapi4j"
  version = "0.6" + (Boolean.valueOf(System.getProperty("release")) ? "" : "-SNAPSHOT")

  java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }
  compileJava.options.encoding = StandardCharsets.UTF_8.toString()

  tasks.withType(JavaCompile) {
    options.encoding = StandardCharsets.UTF_8.toString()
  }

  repositories {
    mavenCentral()
    mavenLocal()
  }

  /*tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
  }*/

  jar {
    manifest {
      attributes 'Build-date': new Date()
      attributes 'Implementation-Title': project.name
      attributes 'Implementation-Version': project.version
    }
  }
}

// Only report code coverage for published projects
def publishedProjects = subprojects.findAll { it.path != ':openapi-perf-checker' }

task jacocoMerge(type: JacocoMerge) {
  publishedProjects.each { subproject ->
    executionData subproject.tasks.withType(Test)
  }
  doFirst {
    executionData = files(executionData.findAll { it.exists() })
  }
}

task codeCoverageReport(type: JacocoReport) {
  dependsOn publishedProjects.test, jacocoMerge

  additionalSourceDirs.from = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  sourceDirectories.from = files(publishedProjects.sourceSets.main.allSource.srcDirs)
  classDirectories.from = files(publishedProjects.sourceSets.main.output)
  executionData jacocoMerge.destinationFile

  reports {
    xml.enabled true
    html.enabled true
    csv.enabled false
  }
}

sonarqube {
  properties {
    property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/codeCoverageReport/codeCoverageReport.xml"
  }
}
